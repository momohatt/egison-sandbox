(define $digit?
  (lambda [$c] (c.between? c#0 c#9 c)))
(define $whitespace?
  (lambda [$c] (any (eq? $ c) {c#  c#\n c#\t})))

(define $expr
  (algebraic-data-matcher
    {<lam string expr>
     <app expr expr>
     <var string>
     <add expr expr>
     <sub expr expr>
     <mult expr expr>
     <div expr expr>
     <int integer>}))

(define $L.number (pattern-function [$x $p] <parse-number x <sc p>>))
(define $L.char   (pattern-function [$x $p] <parse-char   x <sc p>>))
(define $L.string (pattern-function [$x $p] <parse-string x <sc p>>))
(define $L.ident  (pattern-function [$x $p] <parse-ident  x <sc p>>))

(define $leftOpExpr1
  (lambda [$acc $tgt]
    (match tgt parser
      {[(L.char ,c#+ <opExpr2 $n $p>) (leftOpExpr1 <Add acc n> p)]
       [(L.char ,c#- <opExpr2 $n $p>) (leftOpExpr1 <Sub acc n> p)]
       [_ {[acc tgt]}]})))

(define $leftOpExpr2
  (lambda [$acc $tgt]
    (match tgt parser
      {[(L.char ,c#* <atomExpr $n $p>) (leftOpExpr2 <Mult acc n> p)]
       [(L.char ,c#/ <atomExpr $n $p>) (leftOpExpr2 <Div acc n> p)]
       [_ {[acc tgt]}]})))

(define $parser
  (matcher
    {[<expr' $ $> [expr parser]
      {[$tgt (match tgt parser
               {[<opExpr1 $n $p>    {[n p]}]
                [<lambdaExpr $n $p> {[n p]}]
                [_ {}]})]}]
     [<lambdaExpr $ $> [expr parser]
      {[$tgt (match tgt parser
               {[(L.char ,c#\\ (L.ident $x (L.string ,"->" <expr' $e $p>)))
                 {[<Lam x e> p]}]
                [_ {}]})]}]

     [<opExpr1 $ $> [expr parser]
      {[$tgt (match tgt parser
               {[<opExpr2 $n $p> (leftOpExpr1 n p)]
                [_ {}]})]}]
     [<opExpr2 $ $> [expr parser]
      {[$tgt (match tgt parser
               {[<atomExpr $n $p> (leftOpExpr2 n p)]
                [_ {}]})]}]
     [<atomExpr $ $> [expr parser]
      {[$tgt (match tgt parser
               {[(L.number $n $p) {[<Int n> p]}]
                [(L.ident $n $p)  {[<Var n> p]}]
                [(L.string ,"(" <expr' $n (L.string ,")" $p)>) {[n p]}]
                [_ {}]})]}]

     ;; literals
     [<parse-number $ $> [integer parser]
      {[$tgt (match tgt string
               {[<regex ,"[0-9]+" <nil> $num $r> {[(read num) r]}]
                [_ {}]})]}]
     [<parse-char $ $> [char parser]
      {[$tgt (match tgt string
               {[<cons $x $xs> {[x xs]}]
                [_ {}]})]}]
     [<parse-string $ $> [string parser]
      {[$tgt (match-all tgt string
               {[<join $xs $ys> [xs ys]]})]}]
     [<parse-ident $ $> [string parser]
      {[$tgt (match tgt string
               {[<regex ,"[A-Za-z]+" <nil> $x $r> {[x r]}]
                [_ {}]})]}]

     [<sc $> [parser]
      {[$tgt (match tgt string
               {[(loop $i [1 _]
                       <cons ?whitespace? ...>
                       (& $r !<cons ?whitespace? _>))
                 {r}]
                [_ {}]})]}]
     [<eof> []
      {[$tgt (if (S.empty? tgt) {[]} {})]}]
     [$ [something]
      {[$tgt {tgt}]}]
     }))

(assert-equal "parser"
  (match "123 4" parser {[(L.number $n (L.number $m <eof>)) [n m]]})
  [123 4])
(assert-equal "parser"
  (match "123 4" parser {[(L.number $n (L.number ,4 <eof>)) n]})
  123)
(assert-equal "parser"
  (match "123 c" parser {[(L.number $n (L.char $c <eof>)) [n c]]})
  [123 c#c])
(assert-equal "parser"
  (match "123 c" parser {[(L.number $n (L.string ,"c" <eof>)) n]})
  123)
(assert-equal "parser"
  (match "1 + 2 + 3" parser {[<expr' $n <eof>> n]})
  <Add <Add <Int 1> <Int 2>> <Int 3>>)
(assert-equal "parser"
  (match "1 + 2 * 3" parser {[<expr' $n <eof>> n]})
  <Add <Int 1> <Mult <Int 2> <Int 3>>>)
(assert-equal "parser"
  (match "(1 + 2) * 3" parser {[<expr' $n <eof>> n]})
  <Mult <Add <Int 1> <Int 2>> <Int 3>>)
(assert-equal "parser"
  (match "\\x -> \\y -> x + y" parser {[<expr' $n <eof>> n]})
  <Lam "x" <Lam "y" <Add <Var "x"> <Var "y">>>>)
