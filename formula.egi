(define $symbol something)

(define $monomial
  (matcher
    {[<degree $> [integer]
      {[$tgt {(length tgt)}]}]
     [<power ,$x $> [integer]
      {[$tgt (match tgt (multiset symbol)
               {[(loop $i [1 $n]
                       <cons ,x ...>
                       !<cons ,x _>) {n}]})]}]
     [<power $ $> [symbol integer]
      {[$tgt (match-all [(unique tgt) tgt] [(multiset symbol) monomial]
               {[[<cons $x _> <power ,x $n>] [x n]]})]}]
     [<mult ,$val $> [monomial]
      {[$tgt (match-all tgt (multiset symbol)
               [<join ,val $rest> rest])]}]
     [<mult $ $> [monomial monomial]
      ; TODO: is there more efficient implementation without unique?
      {[$tgt (unique (match-all tgt (multiset symbol)
                       [<join $xs $ys> [xs ys]]))]}]
     [$ (multiset symbol)
      {[$tgt {tgt}]}]
     }))

(define $polynomial
  (matcher
    {[<occur $> [monomial]
      {[$tgt (map snd tgt)]}]
     [<head $ $> [integer monomial]
      {[$tgt {(max/fn (lambda [$m1 $m2] (ord-mono (snd m1) (snd m2))) tgt)}]}]
     [$ (multiset [integer monomial])
      {[$tgt {tgt}]}]
     }))

(define $ord-mono
  (match-lambda [monomial monomial]
    {[[<degree $n1> <degree $n2>]
      (compare n1 n2)]}))

(define $non-heads
  (lambda [$poly]
    (match poly polynomial
      {[<head $c $mono> (delete-first [c mono] poly)]})))

(define $negate
  (match-all-lambda polynomial
    {[<cons [$c $mono] _> [(neg c) mono]]}))

(define $mult
  (match-all-lambda [[integer monomial] polynomial]
    {[[[$c1 $m1] <cons [$c2 $m2] _>]
      [(* c1 c2) {@m1 @m2}]]
     }))

(define $plus
  (match-all-lambda [polynomial polynomial]
    {[[<cons [$c1 $m1] _> !<cons [_ ,m1] _>] [c1 m1]]
     [[<cons [$c1 $m1] _> <cons [(& !,(neg c1) $c2) ,m1] _>] [(+ c1 c2) m1]]
     [[(later !<cons [_ ,m2] _>) <cons [$c2 $m2] _>] [c2 m2]]
     }))

(assert "plus"
  (match (plus {[2 {x y}] [-1 {x}]} {[1 {x y}] [-1 {y}]}) (multiset something)
    {[,{[3 {x y}] [-1 {x}] [-1 {y}]} #t]}))

(define $rewrite
  (lambda [$eqs $poly]
    (match [eqs poly] [(multiset polynomial) polynomial]
      {[[<cons (& $rule <head ,1 $hf>) _> <cons [$a <mult ,hf $m'>] $rs>]
        (plus rs (mult [a m'] (negate (non-heads rule))))]
       [_ poly]})))

(define $normalize
  (lambda [$eqs $poly]
    (let {[$poly' (rewrite eqs poly)]}
      (if (eq?/m polynomial poly poly') poly (normalize eqs poly')))))

(assert-equal "rewrite"
  (rewrite {f1 f2} {[1 {x x y y}]})
  {[1 {y x x}]})

; ========================== Buchberger's algorithm ============================

(define $replicate
  (lambda [$n $x] (take n (repeat1 x))))

(define $lcm
  (lambda [$m1 $m2]
    (concat
      (match-all [m1 m2] [monomial monomial]
        {[[<power $x $n1> <power ,x $n2>] (replicate (max {n1 n2}) x)]
         [[(later <power ,x ,0>) <power $x $n>] (replicate n x)]}))))

(define $s-polynomial
  (lambda [$f1 $f2]
    (match [f1 f2] [polynomial polynomial]
      {[[<head ,1 $h1> <head ,1 $h2>]
        (match (lcm h1 h2) monomial
          {[(& <mult ,h1 $m1> <mult ,h2 $m2>)
            (plus (mult [1 m1] f1) (mult [-1 m2] f2))]})]})))

(assert-equal "s-polynomial"
  (s-polynomial f1 f2)
  {[-1 {y x x}] [1 {x y y}]})

(define $buchberger-loop
  (lambda [$pairs $fs]
    (match pairs (list [polynomial polynomial])
      {[<nil> fs]
       [<cons [$p1 $p2] $pairs'>
        (let* {[$s (s-polynomial p1 p2)]
               [$h (normalize fs s)]}
          (match h polynomial
            {[<nil> (buchberger-loop pairs' fs)]
             [<head $c _>
              (let* {[$h' (mult [(/ 1 c) {}] h)]
                     [$new-pairs (map (lambda $x [x h']) fs)]}
                (buchberger-loop {@pairs' @new-pairs}
                                 (cons (debug/fn show-poly h') fs)))]}))]
       })))

(define $all-pairs
  (lambda [$fs]
    (match-all fs (list something)
      {[<join _ <cons $f1 <join _ <cons $f2 _>>>> [f1 f2]]})))

(define $buchberger
  (lambda [$fs]
    (buchberger-loop (all-pairs fs) fs)))

; ==============================================================================

(define $f1 {[1 {x x y}] [-1 {x x}]}) ; x^2 y - x^2
(define $f2 {[1 {x y y}] [-1 {y y}]}) ; x y^2 - y^2

; 2 x^2 y - x y^2 + 5 x + 17
(define $f3 {[2 {x x y}] [-1 {x y y}] [5 {x}] [17 {}]})

(assert-equal "occur"
  (match-all f3 polynomial [<occur $m> m])
  {{x x y} {x y y} {x} {}})

; ==============================================================================

(define $show-mono
  (lambda [$mono]
    (S.intercalate " "
      (match-all mono monomial
        {[<power $x $n>
          (if (eq? n 1)
              (show x)
              (S.concat {(show x) "^" (show n)}))]
         [<nil> "1"]}))))

(define $show-poly
  (lambda [$poly]
    (S.intercalate " + "
       (map
         (match-lambda [integer monomial]
           {[[,1 $x] (show-mono x)]
            [[,-1 $x] (S.append "-" (show-mono x))]
            [[$c $x] (S.append (show c) (show-mono x))]})
         poly))))

(define $debug/fn
  (macro [$show $x]
    (io (do {[(print (show x))]}
          (return x)))))
