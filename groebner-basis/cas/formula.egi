(load-file "example.egi")

(define $symbol something)

(define $monomial
  (matcher
    {[<degree $> [integer]
      {[$tgt {(sum (match-all tgt math-expr
                    {[<term _ <ncons _ $x _>> x]}))}]}]
     [<mult ,$val $> [monomial]
      {[$tgt (match (/ tgt val) math-expr
               {[<div _ !,1> {}]
                [$rem {rem}]})]}]
     [<power $ $> [symbol integer]
      {[$tgt (match-all tgt math-expr
               {[<term _ <ncons $x $n _>> [x n]]})]}]
     [$ math-expr
      {[$tgt {tgt}]}]
     }))

(assert-equal "degree"
  (match 1 monomial {[<degree $n> n]})
  0)
(assert-equal "degree"
  (match (* x^2 y) monomial {[<degree $n> n]})
  3)
(assert-equal "power"
  (match-all (* x^2 y) monomial {[<power $x $n> [x n]]})
  {[x 2] [y 1]})

(define $polynomial
  (matcher
    {[<head ,$ord $ $> [integer monomial]
      {[$tgt
         (let {[$monos (match-all tgt math-expr
                         {[<poly <cons <mult $n $x> _>> [n x]]})]}
          {(max/fn (lambda [$m1 $m2] (ord-mono (snd m1) (snd m2) ord)) monos)})
        ]}]
     [<cons $ $> [monomial polynomial]
      {[$tgt (match-all tgt math-expr
               {[<poly <cons $x $y>> [x (- tgt x)]]})]}]
     [$ math-expr
      {[$tgt {tgt}]}]
     }))

(assert-equal "head"
  (match (+ x x^2) polynomial {[<head ,{x} $n $x> [n x]]})
  [1 x^2])

(define $ord-mono-lex   ; lexicographic order
  (lambda [$l $m1 $m2]
    (match [l m1 m2] [(list symbol) monomial monomial]
      {[[<nil> _ _] <Equal>]
       [[<cons $x $xs> <power ,x $n1> <power ,x $n2>]
        (if (eq? n1 n2)
            (ord-mono-lex xs m1 m2)
            (compare n1 n2))]})))

(define $ord-mono
  (match-lambda [monomial monomial (list symbol)]
    {[[<degree $n1> <degree (& !,n1 $n2)> _]
      (compare n1 n2)]
     [[$m1 $m2 $sym-list]
      (ord-mono-lex sym-list m1 m2)]}))

(assert-equal "ord-mono" (ord-mono x^2 x^2 {x}) <Equal>)
(assert-equal "ord-mono" (ord-mono x^2 (* x y) {x y}) <Greater>)

(define $non-heads
  (lambda [$poly $ord]
    (match poly polynomial
      {[<head ,ord $c $mono> (- poly (* c mono))]})))

(assert-equal "non-heads"
  (non-heads (+ x^2 y 1) {x y})
  (+ y 1))

(define $rewrite
  (lambda [$eqs $poly $ord]
    (match [eqs poly] [(multiset polynomial) polynomial]
      {[[<cons (& $rule <head ,ord ,1 $hf>) _>
         <cons <mult ,hf $m'> $rs>]
        (+ rs (* m' (neg (non-heads rule ord))))]
       [_ poly]})))

(define $normalize
  (lambda [$eqs $poly $ord]
    (let {[$poly' (rewrite eqs poly ord)]}
      (if (eq? poly poly') poly (normalize eqs poly' ord)))))

(define $joinable?
  (lambda [$eqs $poly $ord]
    (eq? (normalize eqs poly ord) {})))

(assert-equal "rewrite"
  (rewrite {f1 f2} (* x^2 y^2) {x y})
  (* x^2 y))

; ========================== Buchberger's algorithm ============================

(define $replicate
  (lambda [$n $x] (take n (repeat1 x))))

; TODO: fix

(define $lcm
  (lambda [$m1 $m2]
    (concat
      (match-all [m1 m2] [monomial monomial]
        {[[<power $x $n1> <power ,x $n2>] (replicate (max {n1 n2}) x)]
         [[(later <power ,x ,0>) <power $x $n>] (replicate n x)]}))))

(define $s-polynomial
  (lambda [$f1 $f2 $ord]
    (match [f1 f2] [polynomial polynomial]
      {[[<head ,ord ,1 $h1> <head ,ord ,1 $h2>]
        (match (lcm h1 h2) monomial
          {[(& <mult ,h1 $m1> <mult ,h2 $m2>)
            (plus (mult [1 m1] f1) (mult [-1 m2] f2))]})]
       [[<head ,ord $c1 $h1> <head ,ord $c2 $h2>]
        (s-polynomial (mult [(/ 1 c1) {}] f1) (mult [(/ 1 c2) {}] f2) ord)]
        })))

(assert-equal "s-polynomial"
  (s-polynomial f1 f2 {x y})
  {[-1 {y x x}] [1 {x y y}]})

(define $buchberger-loop
  (lambda [$pairs $fs $ord]
    (match pairs (list [polynomial polynomial])
      {[<nil> (interreduce fs ord)]
       [<cons [$p1 $p2] $pairs'>
        (let {[$h (normalize fs (s-polynomial p1 p2 ord) ord)]}
          (match h polynomial
            {[<nil> (buchberger-loop pairs' fs ord)]
             [<head ,ord $c _>
              (let* {[$h' (mult [(/ 1 c) {}] h)]
                     [$new-pairs (map (lambda $x [x h']) fs)]}
                (buchberger-loop {@pairs' @new-pairs}
                                 (cons (debug/fn show-poly h') fs)
                                 ord))]
             }))]
       })))

(define $all-pairs
  (lambda [$fs]
    (match-all fs (list something)
      {[<join _ <cons $f1 <join _ <cons $f2 _>>>> [f1 f2]]})))

(define $interreduce
  (lambda [$fs $ord]
    (match fs (multiset polynomial)
      {[<cons $f (& ?(joinable? $ f ord) $other)>
        (interreduce other ord)]
       [_ fs]})))

(define $buchberger
  (lambda [$fs $ord]
    (buchberger-loop (all-pairs fs) fs ord)))

; ==============================================================================

(define $show-mono
  (lambda [$mono]
    (S.intercalate " "
      (match-all mono monomial
        {[<power $x $n>
          (if (eq? n 1)
              (show x)
              (S.concat {(show x) "^" (show n)}))]
         [<nil> "1"]}))))

(define $show-coeff-mono
  (lambda [$c $x]
    (match [c x] [integer monomial]
      {[[,1         <nil>] "+ 1"]
       [[,-1        <nil>] "- 1"]
       [[?(gt? $ 0) <nil>] (S.append "+ " (show c))]
       [[?(lt? $ 0) <nil>] (S.append "- " (show (neg c)))]
       [[,1         $x]    (S.append "+ " (show-mono x))]
       [[,-1        $x]    (S.append "- " (show-mono x))]
       [[?(gt? $ 0) $x]    (S.concat {"+ " (show c) (show-mono x)})]
       [[?(lt? $ 0) $x]    (S.concat {"- " (show (neg c)) (show-mono x)})]
       })))

(define $show-coeff-mono-head
  (lambda [$c $x]
    (match [c x] [integer monomial]
      {[[,1         <nil>] "1"]
       [[,-1        <nil>] "- 1"]
       [[?(gt? $ 0) <nil>] (show c)]
       [[?(lt? $ 0) <nil>] (S.append "- " (show (neg c)))]
       [[,1         $x]    (show-mono x)]
       [[,-1        $x]    (S.append "- " (show-mono x))]
       [[?(gt? $ 0) $x]    (S.append (show c) (show-mono x))]
       [[?(lt? $ 0) $x]    (S.concat {"- " (show (neg c)) (show-mono x)})]
       })))

(define $show-poly
  (match-lambda (list [integer monomial])
    {[<nil> "0"]
     [<cons $x $xs>
      (let {[$first (show-coeff-mono-head x)]}
        (S.intercalate " " {first @(map show-coeff-mono xs)}))]}))

(define $debug/fn
  (macro [$show $x]
    (io (do {[(print (show x))]}
          (return x)))))
